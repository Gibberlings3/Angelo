IF
NumTimesTalkedTo(0)
See([PC])
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF
Global("ADSawaraBetray","GLOBAL",2)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF
OR(4)
AttackedBy([GOODCUTOFF],DEFAULT)
PartyHasItem("ADClub")
PartyHasItem("ADDiary")
PartyHasItem("ADFinger")
Allegiance(Myself,NEUTRAL)
Global("ADAngelSawaraAttacked","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("ADAngelSawaraAttacked","LOCALS",1)
SetGlobal("ADAngelPCAttackedSawara","GLOBAL",1)
ActionOverride("ADFox",DestroySelf())
Enemy()
END

IF
OR(2)
Global("ADAngelPCAttackedSawara","GLOBAL",1)
Global("ADAngelGrandDuel","GLOBAL",1)
Allegiance(Myself,NEUTRAL)
Global("ADAngelSawaraHostile","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("ADAngelSawaraHostile","LOCALS",1)
ActionOverride("ADFox",DestroySelf())
Enemy()
END

IF
See(NearestEnemyOf(Myself))
Global("Prep","LOCALS",0)
THEN
RESPONSE #100
ApplySpell(Myself,WIZARD_STONE_SKIN)
ApplySpell(Myself,WIZARD_SPELL_TRAP)
ApplySpell(Myself,WIZARD_MIRROR_IMAGE)
ApplySpell(Myself,WIZARD_SPELL_TURNING)
ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
SetGlobal("Prep","LOCALS",1)
END

IF
See(NearestEnemyOf(Myself))
StateCheck(Myself,STATE_SILENCED)
HaveSpell(WIZARD_VOCALIZE)
THEN
RESPONSE #100
Spell(Myself,WIZARD_VOCALIZE)
END

IF
See(NearestEnemyOf(Myself))
CheckStatLT(Myself,1,STONESKINS)
TookDamage()
Global("Prep2","LOCALS",0)
THEN
RESPONSE #100
ApplySpell(Myself,WIZARD_SPELL_TURNING)
ApplySpell(Myself,WIZARD_MISLEAD)
ApplySpell(Myself,WIZARD_STONE_SKIN)
SetGlobal("Prep2","LOCALS",1)
END

IF
See(NearestEnemyOf(Myself))
CheckStatLT(Myself,1,STONESKINS)
TookDamage()
Global("Prep2","LOCALS",1)
THEN
RESPONSE #100
ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
SetGlobal("Prep2","LOCALS",2)
END

IF
!HasBounceEffects(Myself)
!HasImmunityEffects(Myself)
See(NearestEnemyOf(Myself))
THEN
RESPONSE #100
ForceSpell(Myself,WIZARD_SPELL_TURNING)
END

IF
See(NearestEnemyOf(Myself))
HaveSpell(WIZARD_TIME_STOP)
THEN
RESPONSE #100
Spell(Myself,WIZARD_TIME_STOP)
SpellNoDec(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
SpellNoDec(SecondNearestEnemyOf(Myself),WIZARD_FINGER_OF_DEATH)
SpellNoDec(Myself,WIZARD_TIME_STOP)
SpellNoDec(ThirdNearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
SpellNoDec(ThirdNearestEnemyOf(Myself),WIZARD_NPC_SYMBOL_STUN)
SpellNoDec(Myself,WIZARD_TIME_STOP)
SpellNoDec(ThirdNearestEnemyOf(Myself),WIZARD_SUMMON_EFREET)
SpellNoDec(ThirdNearestEnemyOf(Myself),WIZARD_CHAIN_LIGHTNING)
END

IF
Allegiance(Myself,ENEMY)
See([PC])
HPLT([PC],60)
GlobalLT("KILL","LOCALS",5)
THEN
RESPONSE #100
SpellNoDec([PC],WIZARD_POWER_WORD_KILL)
IncrementGlobal("KILL","LOCALS",1)
END

IF
See(NearestEnemyOf(Myself))
Global("SpellTrigger","LOCALS",0)
THEN
RESPONSE #100
DisplayStringHead(Myself,~Spell Trigger - Fired~)
ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_TRUE_SIGHT)
ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_DISPEL_MAGIC)
ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_DISINTEGRATE)
SetGlobal("SpellTrigger","LOCALS",1)
END

IF
See(NearestEnemyOf(Myself))
THEN
RESPONSE #20
SpellNoDec(NearestEnemyOf(Myself),WIZARD_FINGER_OF_DEATH)
RESPONSE #20
SpellNoDec(ThirdNearestEnemyOf(Myself),WIZARD_CHAOS)
RESPONSE #20
SpellNoDec(FourthNearestEnemyOf(Myself),WIZARD_CHAIN_LIGHTNING)
RESPONSE #20
SpellNoDec(FifthNearestEnemyOf(Myself),WIZARD_POWER_WORD_STUN)
RESPONSE #20
SpellNoDec(SixthNearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END
