// Hi
IF
See(Player1)
Global("ADMetAngelo","GLOBAL",0)
Global("ADAngelSaidHi","GLOBAL",0)
THEN
RESPONSE #100
DisplayStringHead(Myself,~Hi, friend.~)
SetGlobal("ADAngelSaidHi","GLOBAL",1)
END

// Attacked
IF
!InParty(Myself)
AttackedBy([GOODCUTOFF],DEFAULT)
Allegiance(Myself,NEUTRAL)
Global("ADAngelPCAttacked","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("ADAngelPCAttacked","GLOBAL",1)
Enemy()
END

// Experience fixer
IF
InParty(Myself)
Global("ADAngelExperienceSet","GLOBAL",0)
XPLT(Player1,280001)
THEN
RESPONSE #100
SetGlobal("ADAngelExperienceSet","GLOBAL",1)
END

// 400000
IF
InParty(Myself)
Global("ADAngelExperienceSet","GLOBAL",0)
XPGT(Player1,280000)
XPLT(Player1,400001)
THEN
RESPONSE #100
SetGlobal("ADAngelExperienceSet","GLOBAL",1)
AddXPObject("ADAngel",239000)
END

// 800000
IF
InParty(Myself)
Global("ADAngelExperienceSet","GLOBAL",0)
XPGT(Player1,600000)
XPLT(Player1,1000001)
THEN
RESPONSE #100
SetGlobal("ADAngelExperienceSet","GLOBAL",1)
AddXPObject("ADAngel",639000)
END

// 1200000
IF
InParty(Myself)
Global("ADAngelExperienceSet","GLOBAL",0)
XPGT(Player1,1000000)
XPLT(Player1,1400001)
THEN
RESPONSE #100
SetGlobal("ADAngelExperienceSet","GLOBAL",1)
AddXPObject("ADAngel",1039000)
END

// Anomen script kill
IF 
Global("ADAngelWonAnomen","GLOBAL",0)
Global("ADAngelRomanceActive","GLOBAL",2)
OR(2)
Global("AnomenRomanceActive","GLOBAL",1)
Global("AnomenRomanceActive","GLOBAL",2)
THEN
RESPONSE #100
SetGlobal("ADAngelWonAnomen","GLOBAL",1)
SetGlobal("AnomenRomanceActive","GLOBAL",3)
END

IF 
Global("ADAngelLostAnomen","GLOBAL",0)
Global("AnomenRomanceActive","GLOBAL",2)
Global("ADAngelRomanceActive","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("ADAngelLostAnomen","GLOBAL",1)
SetGlobal("ADAngelRomanceActive","GLOBAL",3)
END

// Hotkey fixer
IF
HotKey(K)
THEN
RESPONSE #100
StartCutSceneMode()
StartCutScene("ADAngelF")
END

// Shar-Teel talk
IF
InParty(Myself)
GlobalGT("ADAngelPIDMore","GLOBAL",0)
Global("ADAngelDaughterTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("ADAngelTimer","GLOBAL",3600)
SetGlobal("ADAngelDaughterTalk","GLOBAL",1)
END

IF
InParty(Myself)
Global("ADAngelDaughterTalk","GLOBAL",1)
RealGlobalTimerExpired("ADAngelTimer","GLOBAL")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelDaughterTalk","GLOBAL",2)
END

IF
Global("ADAngelDaughterTalk","GLOBAL",2)
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Undead
IF
Global("ADAngelUndead","GLOBAL",0)
InParty(Myself)
AreaCheck("AR0802")
!Global("ADAngSakakiTalk","GLOBAL",100)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelUndead","GLOBAL",1)
END

IF
Global("ADAngelUndead","GLOBAL",1)
InParty(Myself)
AreaCheck("AR0802")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Spiders
IF
Global("ADAngelSpiders","GLOBAL",0)
InParty(Myself)
AreaCheck("AR0804")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelSpiders","GLOBAL",1)
END

IF
Global("ADAngelSpiders","GLOBAL",1)
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Romance
IF
InParty(Myself)
Global("ADAngelFlirt","GLOBAL",2)
GlobalGT("ADAngelDaughterTalk","GLOBAL",2)
Global("ADAngelRomanceActive","GLOBAL",0)
Global("ADAngelLoveTalk","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("ADAngelLoveTalk","GLOBAL",1)
SetGlobal("ADAngelRomanceActive","GLOBAL",1)
END

IF
InParty(Myself)
Global("ADAngelRomanceActive","GLOBAL",1)
RealGlobalTimerExpired("ADAngelTimer","GLOBAL")
!AreaType(DUNGEON)
See(Player1)
Global("ADAngelLoveTalk","GLOBAL",3)
PartyRested()
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelLoveTalk","GLOBAL",4)
END

IF
InParty(Myself)
RealGlobalTimerExpired("ADAngelTimer","GLOBAL")
GlobalLT("ADAngelLoveTalk","GLOBAL",27)
OR(11)
Global("ADAngelLoveTalk","GLOBAL",4)
Global("ADAngelLoveTalk","GLOBAL",6)
Global("ADAngelLoveTalk","GLOBAL",8)
Global("ADAngelLoveTalk","GLOBAL",10)
Global("ADAngelLoveTalk","GLOBAL",14)
Global("ADAngelLoveTalk","GLOBAL",16)
Global("ADAngelLoveTalk","GLOBAL",18)
Global("ADAngelLoveTalk","GLOBAL",20)
Global("ADAngelLoveTalk","GLOBAL",22)
Global("ADAngelLoveTalk","GLOBAL",24)
Global("ADAngelLoveTalk","GLOBAL",26)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("adang")
StartDialogueNoSet(Player1)
END

IF
InParty(Myself)
OR(2)
Global("ADAngelRomanceActive","GLOBAL",1)
Global("ADAngelRomanceActive","GLOBAL",2)
RealGlobalTimerExpired("ADAngelTimer","GLOBAL")
GlobalLT("ADAngelLoveTalk","GLOBAL",27)
!AreaType(DUNGEON)
OR(10)
Global("ADAngelLoveTalk","GLOBAL",5)
Global("ADAngelLoveTalk","GLOBAL",7)
Global("ADAngelLoveTalk","GLOBAL",9)
Global("ADAngelLoveTalk","GLOBAL",13)
Global("ADAngelLoveTalk","GLOBAL",15)
Global("ADAngelLoveTalk","GLOBAL",17)
Global("ADAngelLoveTalk","GLOBAL",19)
Global("ADAngelLoveTalk","GLOBAL",21)
Global("ADAngelLoveTalk","GLOBAL",23)
Global("ADAngelLoveTalk","GLOBAL",25)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
IncrementGlobal("ADAngelLoveTalk","GLOBAL",1)
END

// Last neutral talk
IF
InParty(Myself)
Global("ADAngelRomanceActive","GLOBAL",2)
Global("ADAngelLoveTalk","GLOBAL",27)
RealGlobalTimerExpired("ADAngelTimer","GLOBAL")
See(Player1)
!Alignment(Player1,MASK_EVIL)
!AreaType(DUNGEON)
Global("ADAngelGoodTalk","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelGoodTalk","GLOBAL",1)
END

IF
Global("ADAngelGoodTalk","GLOBAL",1)
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("adang")
StartDialogueNoSet(Player1)
END

// Butterfly flirt
IF
InParty(Myself)
Global("ADAngelRomanceActive","GLOBAL",2)
RealGlobalTimerExpired("ADAngelTimer","GLOBAL")
See(Player1)
AreaType(FOREST)
Global("ADAngelButterfly","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
RealSetGlobalTimer("ADAngelButterflyTimer","GLOBAL",20)
SetGlobal("ADAngelButterfly","GLOBAL",1)
END

IF
InParty(Myself)
Global("ADAngelButterfly","GLOBAL",1)
RealGlobalTimerExpired("ADAngelButterflyTimer","GLOBAL")
AreaType(FOREST)
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelButterfly","GLOBAL",2)
END

IF
Global("ADAngelButterfly","GLOBAL",2)
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("adang")
StartDialogueNoSet(Player1)
END

// Race talk
IF
InParty(Myself)
Global("ADAngelRomanceActive","GLOBAL",2)
RealGlobalTimerExpired("ADAngelTimer","GLOBAL")
See(Player1)
GlobalGT("ADAngelLoveTalk","GLOBAL",26)
Global("ADAngelRaceTalk","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
!AreaType(DUNGEON)
!Race(Player1,HUMAN)
!Race(Player1,HALF_ELF)
!Race(Player1,GNOME)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelRaceTalk","GLOBAL",1)
END

IF
Global("ADAngelRaceTalk","GLOBAL",1)
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("adang")
StartDialogueNoSet(Player1)
END

// Flirts
IF
InParty(Myself)
GlobalGT("ADAngelLoveTalk","GLOBAL",4)
Global("ADAngelFlirtsActive","GLOBAL",0)
!Global("ADAngelRomanceActive","GLOBAL",3)
THEN
RESPONSE #100
SetGlobal("ADAngelFlirtsActive","GLOBAL",1)
END

IF 
InParty(Myself)
Global("ADAngelFlirtsActive","GLOBAL",1)
Global("ADAngelRomanceActive","GLOBAL",3)
THEN
RESPONSE #100
SetGlobal("ADAngelFlirtsActive","GLOBAL",0)
END

// PC slept with Phaere
IF
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
See(Player1)
Global("PhaereInnuendo","GLOBAL",2)
!Range("Phaere",6)
Global("ADAngelPhaereIn","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("ADAngelPhaereIn","LOCALS",1)
END

IF
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
!StateCheck(Player1,CD_STATE_NOTVALID)
Global("ADAngelPhaereIn","LOCALS",1)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Ambush N1
IF
InParty(Myself)
Global("ADAngelBounty1","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
OR(6)
AreaCheck("AR0300")
AreaCheck("AR0400")
AreaCheck("AR0500")
AreaCheck("AR0700")
AreaCheck("AR1000")
AreaCheck("AR0900")
THEN
RESPONSE #100
StartCutSceneMode()
CreateCreatureObjectOffset("ADAsgurz","ADAngel",[20.20])  
CreateCreatureObjectOffset("ADAsg1","ADAngel",[30.10])  
CreateCreatureObjectOffset("ADAsg2","ADAngel",[10.30])  
SetGlobal("ADAngelBounty1","GLOBAL",1)
EndCutSceneMode()
END

// Ambush N2
IF
InParty("ADAngel")
Global("ADAngelBounty1","GLOBAL",1)
Global("ADAngelBounty2","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
AreaCheck("AR1200")
THEN
RESPONSE #100
StartCutSceneMode()
CreateCreature("ADAngMa1",[3910.1200],0) 
CreateCreature("ADAngMa2",[3920.1190],0) 
CreateCreature("ADAngMa3",[3930.1180],0) 
CreateCreature("ADAngMa4",[3940.1170],0) 
SetGlobal("ADAngelBounty2","GLOBAL",1)
EndCutSceneMode()
END

// Ambush N3
IF
InParty("ADAngel")
Global("ADAngelBounty1","GLOBAL",1)
Global("ADAngelBounty2","GLOBAL",1)
Global("ADAngelBounty3","GLOBAL",0)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
AreaCheck("AR0300")
THEN
RESPONSE #100
StartCutSceneMode()
CreateCreature("ADMerzer",[2179.456],0) 
CreateCreature("ADMerz1",[2067.446],0) 
CreateCreature("ADMerz2",[2220.431],0) 
CreateCreature("ADMerz3",[2200.480],0) 
CreateCreature("ADMerz4",[2040.498],0) 
CreateCreature("ADMerz5",[2130.474],0) 
RealSetGlobalTimer("ADAngelAmbush","GLOBAL",3600)
SetGlobal("ADAngelBounty3","GLOBAL",1)
EndCutSceneMode()
END

// After Ambush N1
IF
Global("ADAngelBountyDead1","GLOBAL",0)
Dead("ADAsgurz")
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelBountyDead1","GLOBAL",1)
END

IF
Global("ADAngelBountyDead1","GLOBAL",1)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// After Ambush N2
IF
InParty(Myself)
Global("ADAngelBountyDead2","GLOBAL",0)
Dead("ADAngMa1")
Dead("ADAngMa2")
Dead("ADAngMa3")
Dead("ADAngMa4")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelBountyDead2","GLOBAL",1)
END

IF
Global("ADAngelBountyDead2","GLOBAL",1)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// After Ambush N3
IF
Global("ADAngelBountyDead3","GLOBAL",0)
Dead("ADMerzer")
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelBountyDead3","GLOBAL",1)
END

IF
Global("ADAngelBountyDead3","GLOBAL",1)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Solaufein jealousy in vanilla game
IF
Global("ADAngelInt4","GLOBAL",1)
OR(2)
Global("ADAngelRomanceActive","GLOBAL",1)
Global("ADAngelRomanceActive","GLOBAL",2)
Global("Chapter","GLOBAL",%bg2_chapter_5%)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// QUEST "When the last sword is sheathed"

// Amarant appears to arrest Angelo
IF
InParty(Myself)
RealGlobalTimerExpired("ADAngelAmbush","GLOBAL")
Global("ADAngelBounty1","GLOBAL",1)
Global("ADAngelBounty2","GLOBAL",1)
Global("ADAngelBounty3","GLOBAL",1)
Global("ADAngelAmarantSpawned","GLOBAL",0)
OR(6)
AreaCheck("AR0300")
AreaCheck("AR0400")
AreaCheck("AR0500")
AreaCheck("AR0700")
AreaCheck("AR1000")
AreaCheck("AR0900")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartCutSceneMode()
CreateCreatureObjectOffset("ADAmaG1","ADAngel",[110.130])  
CreateCreatureObjectOffset("ADAmaG2","ADAngel",[130.110])  
CreateCreatureObjectOffset("ADAmara","ADAngel",[120.120])  
SetGlobal("ADAngelAmarantSpawned","GLOBAL",1)
EndCutSceneMode()
END

// Angelo has been freed
IF
Global("ADAngelLeftPrison","GLOBAL",1)
!See("ADSawara")
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Sawara gave the player lotus to deliver, Angelo's reaction
IF
Global("ADAngelLotus","GLOBAL",1)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Player betrayed Sawara
IF
InParty(Myself)
Global("ADSawaraBetray","GLOBAL",1)
AreaType(OUTDOOR)
CombatCounter(0)
!See([ENEMY])
!AreaCheck("AR0300")
!AreaCheck("AR1000")
THEN
RESPONSE #100
StartCutSceneMode()
CreateCreatureObjectOffset("ADSawara",Player1,[20.20])  
CreateCreatureObjectOffset("ADSuu",Player1,[10.30])  
CreateCreatureObjectOffset("ADHayate",Player1,[30.10])  
SetGlobal("ADSawaraBetray","GLOBAL",2)
SetGlobal("ADSawaraIsNotHome","GLOBAL",1)
EndCutSceneMode()
END

// Player betrayed Sawara, Angelo's reaction
IF
Global("ADSawaraBetray","GLOBAL",3)
Dead("ADSawara")
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADSawaraBetray","GLOBAL",4)
END

IF
Global("ADSawaraBetray","GLOBAL",4)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Got the sword from Oliver, Angelo's reaction
IF
Global("ADAngelHaveSawaraSword","GLOBAL",0)
PartyHasItem("ADKatana")
!Dead("ADSawara")
AreaCheck("AR1000")
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("ADAngelHaveSawaraSword","GLOBAL",1)
END

IF
Global("ADAngelHaveSawaraSword","GLOBAL",1)
AreaCheck("AR1000")
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// After the quest is over, if Angelo flirted with Suu
IF
Global("ADAngelSuuJealous","GLOBAL",1)
OR(2)
Global("ADAngelRomanceActive","GLOBAL",1)
Global("ADAngelRomanceActive","GLOBAL",2)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("ADAngelSuuJealous","GLOBAL",2)
END

IF
Global("ADAngelSuuJealous","GLOBAL",2)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// The quest is over and done, out of the area, Angelo's reaction
IF
Global("ADAngelQuestFinale","GLOBAL",0)
Dead("ADSawara")
PartyHasItem("ADKatana")
!AreaCheck("ARAD02")
InParty(Myself)
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
RealSetGlobalTimer("ADAngelAmbush","GLOBAL",300)
SetGlobal("ADAngelQuestFinale","GLOBAL",1)
END

IF
Global("ADAngelQuestFinale","GLOBAL",1)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

// Gin encounter 
IF
InParty(Myself)
RealGlobalTimerExpired("ADAngelAmbush","GLOBAL")
PartyHasItem("ADKatana")
Dead("ADSawara")
AreaType(OUTDOOR)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
Global("ADAngelGinEncounter1","GLOBAL",0)
THEN
RESPONSE #100
StartCutSceneMode()
CreateCreatureObjectOffset("ADGINPAC",Player1,[130.130])  
SetGlobal("ADAngelGinEncounter1","GLOBAL",1)
EndCutSceneMode()
END

// Imogen
IF
InParty(Myself)
Global("ADAngelImogen","GLOBAL",1)
GlobalLT("Chapter","GLOBAL",%bg2_chapter_4%)
OR(10)
AreaCheck("AR0509")
AreaCheck("AR0021")
AreaCheck("AR0406")
AreaCheck("AR0313")
AreaCheck("AR0704")
AreaCheck("AR0709")
AreaCheck("AR0513")
AreaCheck("AR1602")
AreaCheck("AR1105")
AreaCheck("AR2010")
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID) 
!StateCheck(Myself,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartCutSceneMode()
CreateCreatureObjectOffset("ADImogen",Player1,[10.10]) 
SetGlobal("ADAngelImogen","GLOBAL",2)
StartDialogueNoSet(Player1)
END